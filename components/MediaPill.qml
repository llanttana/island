import QtQuick
import Quickshell.Widgets

// Dynamic Island–style "now playing" on the resting pill: the album art in a
// rounded square on the left and a sound wave on the right, tinted with the
// art's own color (the theme accent when there's no art). The clock stays in
// the middle.
Item {
  id: media
  required property var host
  readonly property bool shown: host.mediaPill

  opacity: shown ? 1 : 0
  visible: opacity > 0.01
  Behavior on opacity { NumberAnimation { duration: opacity > 0.5 ? media.host.motionFadeOut : media.host.motionFadeIn; easing.type: media.host.easeCross } }

  ClippingRectangle {
    id: art
    anchors.left: parent.left
    anchors.leftMargin: 8
    anchors.verticalCenter: parent.verticalCenter
    width: 30; height: 30; radius: 8
    color: media.host.withAlpha(media.host.colorAccent, 0.3)
    Image {
      id: artImage
      anchors.fill: parent
      source: media.host.mediaArt
      sourceSize.width: 60
      sourceSize.height: 60
      fillMode: Image.PreserveAspectCrop
      asynchronous: true
      // Browsers rewrite and delete their cover files as tracks change; never
      // hold on to a cached copy of one.
      cache: false
      visible: status === Image.Ready
    }
    Text {
      anchors.centerIn: parent
      visible: artImage.status !== Image.Ready
      text: "󰝚"
      color: media.host.colorAccentText
      font.family: media.host.fontFamily
      font.pixelSize: 14
    }
  }

  SoundWave {
    anchors.right: parent.right
    anchors.rightMargin: 16
    anchors.verticalCenter: parent.verticalCenter
    color: media.host.mediaTint
    playing: media.shown
  }
}
